apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "jp.ttanaka330.androidlearning"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // TODO signingConfig signingConfigs.release
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
        release {
            // TODO リリースビルドのkeystoreを環境変数から取得させる
        }
    }

    sourceSets {
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin

    // Support Library ===============================================
    implementation deps.support.annotations
    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.support.recyclerview
    implementation deps.support.v4

    // Play Services =================================================
    implementation deps.google.licenses

    // Network =======================================================
    implementation deps.retrofit.core
    implementation deps.retrofit.rxjava
    implementation deps.retrofit.gson
    implementation deps.retrofit.picasso
    implementation deps.okhttp3.intercepter

    // Structure =====================================================
    implementation deps.butterknife.core
    kapt deps.butterknife.compiler

    kapt deps.databinding

    implementation deps.dagger.core
    implementation deps.dagger.android
    implementation deps.dagger.androidSupport
    kapt deps.dagger.compiler
    kapt deps.dagger.androidProcessor

    implementation deps.rx.android
    implementation deps.rx.java

    // Debug =========================================================
    implementation deps.timber
    debugImplementation deps.stetho.core
    debugImplementation deps.stetho.http3

    // Test ==========================================================
    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.robolectric
    androidTestImplementation deps.espresso
}
